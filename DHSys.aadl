package DHSys
public
	--SRS 3.4.2.1 (a -> d)sensors and database
	with Database;
	with SensorDevices;
	
	--SRS 4.1.3 RF and Internet
	with RF;
	with Internet;
	
	with PlannerAndReport;
	
	with Actor;
	
	with GUI;
	
	with ApplianceSwitch;
	
	
	system DH
		
	end DH;
	
	system implementation DH.with_devices
		subcomponents
			this_GUI: system UserInterface;
			this_webServer: system WebServ;
			this_dhGateway: system Gateway;
			this_applianceManager: system ApplianceManager;
			this_users: system Users;
			this_sensors: system Sensors;
		connections
			connection1: port this_webServer.webService -> this_GUI.webInterface;
			connection2: port this_users.uDevice -> this_webServer.phoneLaptopPDA;
			connection3: port this_users.uPhysical -> this_dhGateway.gateWayInterface;
			connection4: port this_dhGateway.webServer -> this_webServer.API;
			connection5: port this_dhGateway.appliancePort -> this_applianceManager.interface;
			connection6: port this_dhGateway.rfOutPort -> this_sensors.controller;
			connection7: port this_dhGateway.guiPort -> this_GUI.dhInterface;
	end DH.with_devices;
	
	-------------------------------------------------------------------------------------------------------------------------------
	--Sensors
	system Sensors
		features
			controller: in data port;
	end Sensors;
	
	system implementation Sensors.imp
		--SRS 4.1.1
		--	temperature, humidity, lights, security, and state of small appliances
		subcomponents
			temperature: device SensorDevices::Termostat;
			humidity: device SensorDevices::Humidistat;
			alarmTrigger: device SensorDevices::ContactSwitch;
			sound: device SensorDevices::SoundAlarm;
			light: device SensorDevices::LightAlarm;
			smallDevice: device SensorDevices::PowerSwitch;
			
	end Sensors.imp;
	
   --------------------------------------------------------------------------------------------------------------------------------
   --WebServ

	
	system WebServ
		features
			phoneLaptopPDA: in data port;
			API: in data port;
			webService: out data port;
	end WebServ;
	
	
	system implementation WebServ.impl

		 subcomponents			
			dhDB: system Database::DHDatabase;
			userDB: system Database::UserDatabase;
			
			report: system PlannerAndReport::Report;
			planner: system PlannerAndReport::Planner;
	end WebServ.impl;
	
	----------------------------------------------------------------------------------------------------------------------------
	-- Gateway
	system Gateway
		features
			gateWayInterface: in data port;
			webServer: out data port;
			guiPort: out data port;
			rfOutPort: out data port;
			appliancePort: out data port;
	end Gateway;
	
	system implementation Gateway.imp
		subcomponents
			--SRS 3.4.3.2 
			rfEmitter: device RF::RFEmitter;
			rfReceiver: device RF::RFReceiver;
			
			isp: device Internet::ISP;
	end Gateway.imp;
	
	
	-------------------------------------------------------------------------------------------------------------------------------
	-- ApplianceManager
	
	--SRS 4.5.1 control house lights
	--SRS 4.5.2 can handle 100 ( up to 115 volt 10 amp power switches)
	--SRS 4.5.3 Give status of ON or OFF
	--SRS 4.5.4 Change the status ON/OFF
	--SRS 4.5.5 Manual override until next set state (programmable??)
	
	system ApplianceManager	
		features
			interface: in data port;
	end ApplianceManager;
	
	system implementation ApplianceManager.imp
		subcomponents
			switch1: device ApplianceSwitch::SimpleSwitch;
	end ApplianceManager.imp;
	
	----------------------------------------------------------------------------------------------------------------------------------
	-- Users
	system Users
		features
			uDevice: out data port;
			uPhysical: out data port;
	end Users;
	
	System implementation Users.imp
		subcomponents
			general: system Actor::GeneralUser;
			admin: system Actor::Technician;
	end Users.imp;
	
	----------------------------------------------------------------------------------------------------------------------------------
	-- GUI
	system UserInterface
		features
			webInterface: in data port;
			dhInterface: in data port;
	end UserInterface;
	
	system implementation UserInterface.imp
		subcomponents
			generalInterface: system GUI::GeneralGUI;
			plannerInterface: system GUI::plannerGUI;
	end UserInterface.imp;
	
end DHSys;
